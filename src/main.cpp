// Arduino_GFX's font headers reference an undefined U8G2_FONT_SECTION
// macro — provide a no-op so the const array compiles. The font symbol
// itself is gated on U8G2_USE_LARGE_FONTS (set in build_flags).
#define U8G2_FONT_SECTION(name)
#include <Arduino_GFX_Library.h>

#include "hw/hw.h"
#include "ui_layout.h"
#include <LittleFS.h>
#include <stdarg.h>
#include <esp_mac.h>
#include "ble_bridge.h"
#include "data.h"
#include "buddy.h"

// TFT_eSPI used to define these named colors; Arduino_GFX uses
// RGB565_*. Keep the names so existing UI code compiles unchanged.
#define GREEN  0x07E0
#define RED    0xF800
#define BLUE   0x001F
#define YELLOW 0xFFE0
#define WHITE  0xFFFF
#define BLACK  0x0000

// spr is a thin alias for hwCanvas() — keeps existing UI code unchanged
#define spr (*hwCanvas())

// Advertise as "Claude-XXXX" (last two BT MAC bytes) so multiple sticks
// in one room are distinguishable in the desktop picker. Name persists in
// btName for the BLUETOOTH info page.
static char btName[16] = "Claude";
static void startBt() {
  uint8_t mac[6] = {0};
  esp_read_mac(mac, ESP_MAC_BT);
  snprintf(btName, sizeof(btName), "Claude-%02X%02X", mac[4], mac[5]);
  bleInit(btName);
}

#include "character.h"
#include "stats.h"
const int W = HW_W;
const int H = HW_H;
const int CX = W / 2;
const int CY_BASE = 120;
// LED replaced by AMOLED border-flash via hwBorderAlert() — no GPIO LED.

// Colors used across multiple UI surfaces
const uint16_t HOT   = 0xFA20;   // red-orange: warnings, impatience, deny
const uint16_t PANEL = 0x2104;   // overlay panel background

enum PersonaState { P_SLEEP, P_IDLE, P_BUSY, P_ATTENTION, P_CELEBRATE, P_DIZZY, P_HEART };
const char* stateNames[] = { "sleep", "idle", "busy", "attention", "celebrate", "dizzy", "heart" };

TamaState    tama;
PersonaState baseState   = P_SLEEP;
PersonaState activeState = P_SLEEP;
uint32_t     oneShotUntil = 0;
uint32_t     lastShakeCheck = 0;
float        accelBaseline = 1.0f;
unsigned long t = 0;

// Menu
bool    menuOpen    = false;
uint8_t menuSel     = 0;
uint8_t brightLevel = 4;           // 0..4 → ScreenBreath 20..100
bool    btnALong    = false;

enum DisplayMode { DISP_NORMAL, DISP_PET, DISP_INFO, DISP_COUNT };
uint8_t displayMode = DISP_NORMAL;
uint8_t infoPage = 0;
uint8_t petPage = 0;
const uint8_t PET_PAGES = 2;
uint8_t msgScroll = 0;
uint16_t lastLineGen = 0;
char     lastPromptId[40] = "";
uint32_t lastInteractMs = 0;
bool     dimmed = false;
bool     screenOff = false;
bool     swallowBtnA = false;
bool     swallowBtnB = false;
bool     buddyMode = false;
bool     gifAvailable = false;
const uint8_t SPECIES_GIF = 0xFF;   // species NVS sentinel: use the installed GIF

// Cycle GIF (if installed) → ASCII species 0..N-1 → GIF. Persisted to the
// existing "species" NVS key; 0xFF means GIF mode.
static void nextPet() {
  uint8_t n = buddySpeciesCount();
  if (!buddyMode) {                          // GIF → species 0
    buddyMode = true;
    buddySetSpeciesIdx(0);
    speciesIdxSave(0);
  } else if (buddySpeciesIdx() + 1 >= n && gifAvailable) {  // last species → GIF
    buddyMode = false;
    speciesIdxSave(SPECIES_GIF);
  } else {                                   // species i → species i+1
    buddyNextSpecies();
  }
  characterInvalidate();
  if (buddyMode) buddyInvalidate();
}

static void prevPet() {
  uint8_t n = buddySpeciesCount();
  if (!buddyMode) {                          // GIF → last species
    buddyMode = true;
    buddySetSpeciesIdx(n - 1);
    speciesIdxSave(n - 1);
  } else if (buddySpeciesIdx() == 0 && gifAvailable) {      // first species → GIF
    buddyMode = false;
    speciesIdxSave(SPECIES_GIF);
  } else {
    buddyPrevSpecies();
  }
  characterInvalidate();
  if (buddyMode) buddyInvalidate();
}
uint32_t wakeTransitionUntil = 0;
const uint32_t SCREEN_OFF_MS    = 30UL * 1000UL;        // 30s on battery, non-clock idle
const uint32_t CLOCK_OFF_MS_BAT = 5UL  * 60UL * 1000UL; // 5min on battery, clock visible

bool     napping = false;
uint32_t napStartMs = 0;
uint32_t promptArrivedMs = 0;

// Face-down = Z-axis dominant and negative. Debounced so a toss doesn't count.
static bool isFaceDown() {
  float ax, ay, az;
  hwImuAccel(&ax, &ay, &az);
  return az < -0.7f && fabsf(ax) < 0.4f && fabsf(ay) < 0.4f;
}

static void applyBrightness() { hwDisplayBrightness(brightLevel); }

static void wake() {
  lastInteractMs = millis();
  if (screenOff) {
    hwDisplaySleep(false);
    applyBrightness();
    screenOff = false;
    wakeTransitionUntil = millis() + 12000;
  }
  if (dimmed) { applyBrightness(); dimmed = false; }
}
bool     responseSent = false;

static void beep(uint16_t freq, uint16_t dur) {
  if (settings().sound) hwBeep(freq, dur);
}

// Touch hit-test helper (additive: keys still work, touch is a 2nd path).
// Returns true on a fresh tap-down inside the rect; releases don't fire.
static bool tap(int x, int y, int w, int h) {
  const HwTouch& t = hwTouch();
  return t.justPressed && t.x >= x && t.y >= y && t.x < x+w && t.y < y+h;
}

// Press-start snapshot for gesture classification (swipe). Updated on every
// justPressed; read on justReleased to compute Δx/Δy/Δt.
static int16_t  _tpStartX = 0, _tpStartY = 0;
static uint32_t _tpStartMs = 0;

// Rect hit-test against the press-START position. Use on justReleased after
// a gesture has been classified as a stationary tap — so a tap with minor
// finger drift still targets the region the user pressed on.
static bool tappedFrom(int x, int y, int w, int h) {
  return _tpStartX >= x && _tpStartY >= y &&
         _tpStartX <  x + w && _tpStartY <  y + h;
}

// After a user interaction in clock mode (pet tap or species swipe), keep the
// buddy awake for this long — otherwise the time-of-day logic snaps back to
// P_SLEEP the instant the one-shot animation expires.
static const uint32_t PLAYFUL_MS = 3UL * 60UL * 1000UL;
static uint32_t _playfulUntil = 0;

static void sendCmd(const char* json) {
  Serial.println(json);
  size_t n = strlen(json);
  bleWrite((const uint8_t*)json, n);
  bleWrite((const uint8_t*)"\n", 1);
}
const uint8_t INFO_PAGES = 6;
const uint8_t INFO_PG_BUTTONS = 1;
const uint8_t INFO_PG_CREDITS = 5;

void applyDisplayMode() {
  // Landscape keeps the pet visible on the home screen and hides it behind
  // full-screen Info/Pet panels; portrait keeps the original peek convention.
  bool peek = LANDSCAPE_UI ? (displayMode == DISP_NORMAL)
                           : (displayMode != DISP_NORMAL);
  characterSetPeek(peek);
  buddySetPeek(peek);
  // Clear the whole sprite on mode switch. drawInfo/drawPet clear their
  // own regions when they run, but when you switch FROM info/pet TO normal,
  // those functions stop running and their stale pixels stay behind. Full
  // clear is cheap and guarantees no leftovers between modes.
  spr.fillScreen(0x0000);
  characterInvalidate();  // redraws character on next tick (text mode path)
}

// Swipe cycles through all 9 pages as a flat list:
//   Normal → Pet 1/2 → Pet 2/2 → Info 1/6 → … → Info 6/6 → (wrap to Normal)
// Key1 short-press keeps the coarser 3-mode cycle; these helpers are only
// wired into the release-based gesture classifier below.
// applyDisplayMode() fires on mode transitions and Pet sub-page (matches
// existing BtnB behaviour). Info sub-page skips it because drawInfo() clears
// its own region — also matches existing BtnB behaviour.
static void swipeNextPage() {
  if (displayMode == DISP_NORMAL) {
    displayMode = DISP_PET; petPage = 0;                  applyDisplayMode();
  } else if (displayMode == DISP_PET) {
    if (petPage + 1 < PET_PAGES)    { petPage++;          applyDisplayMode(); }
    else { displayMode = DISP_INFO; infoPage = 0;         applyDisplayMode(); }
  } else { /* DISP_INFO */
    if (infoPage + 1 < INFO_PAGES)  { infoPage++; }
    else { displayMode = DISP_NORMAL;                     applyDisplayMode(); }
  }
}

static void swipePrevPage() {
  if (displayMode == DISP_NORMAL) {
    displayMode = DISP_INFO; infoPage = INFO_PAGES - 1;   applyDisplayMode();
  } else if (displayMode == DISP_PET) {
    if (petPage > 0)                { petPage--;          applyDisplayMode(); }
    else { displayMode = DISP_NORMAL;                     applyDisplayMode(); }
  } else { /* DISP_INFO */
    if (infoPage > 0)               { infoPage--; }
    else { displayMode = DISP_PET; petPage = PET_PAGES - 1; applyDisplayMode(); }
  }
}

const char* menuItems[] = { "settings", "turn off", "help", "about", "demo", "close" };
const uint8_t MENU_N = 6;

bool    settingsOpen = false;
uint8_t settingsSel  = 0;
const char* settingsItems[] = { "brightness", "sound", "bluetooth", "wifi", "led", "transcript", "clock rot", "ascii pet", "reset", "back" };
const uint8_t SETTINGS_N = 10;

bool    resetOpen = false;
uint8_t resetSel  = 0;
const char* resetItems[] = { "delete char", "factory reset", "back" };
const uint8_t RESET_N = 3;
static uint32_t resetConfirmUntil = 0;
static uint8_t  resetConfirmIdx = 0xFF;

static void applySetting(uint8_t idx) {
  Settings& s = settings();
  switch (idx) {
    case 0:
      brightLevel = (brightLevel + 1) % 5;
      applyBrightness();
      return;
    case 1: s.sound = !s.sound; break;
    case 2:
      // BT toggle is a stored preference only — BLE stays live. Turning
      // BLE off cleanly would require tearing down the BLE stack which
      // the Arduino BLE library doesn't do reliably. If we need a
      // hard-off someday, stop advertising via BLEDevice::getAdvertising().
      s.bt = !s.bt;
      break;
    case 3: s.wifi = !s.wifi; break;   // stored only — no WiFi stack linked
    case 4: s.led = !s.led; break;
    case 5: s.hud = !s.hud; break;
    case 6: s.clockRot = (s.clockRot + 1) % 3; break;
    case 7: nextPet(); return;
    case 8: resetOpen = true; resetSel = 0; resetConfirmIdx = 0xFF; return;
    case 9: settingsOpen = false; characterInvalidate(); return;
  }
  settingsSave();
}

// Tap-twice confirm: first tap arms (label flips to "really?"), second
// within 3s executes. Scrolling away clears the arm.
static void applyReset(uint8_t idx) {
  uint32_t now = millis();
  bool armed = (resetConfirmIdx == idx) && (int32_t)(now - resetConfirmUntil) < 0;

  if (idx == 2) { resetOpen = false; return; }

  if (!armed) {
    resetConfirmIdx = idx;
    resetConfirmUntil = now + 3000;
    beep(1400, 60);
    return;
  }

  beep(800, 200);
  if (idx == 0) {
    // delete char: wipe /characters/, reboot into ASCII mode
    File d = LittleFS.open("/characters");
    if (d && d.isDirectory()) {
      File e;
      while ((e = d.openNextFile())) {
        char path[80];
        snprintf(path, sizeof(path), "/characters/%s", e.name());
        if (e.isDirectory()) {
          File f;
          while ((f = e.openNextFile())) {
            char fp[128];
            snprintf(fp, sizeof(fp), "%s/%s", path, f.name());
            f.close();
            LittleFS.remove(fp);
          }
          e.close();
          LittleFS.rmdir(path);
        } else {
          e.close();
          LittleFS.remove(path);
        }
      }
      d.close();
    }
  } else {
    // factory reset: NVS namespace wipe + filesystem format + BLE bonds.
    // Clears stats, owner, petname, species, settings, GIF characters,
    // and any stored LTKs so the next desktop has to re-pair.
    _prefs.begin("buddy", false);
    _prefs.clear();
    _prefs.end();
    LittleFS.format();
    bleClearBonds();
  }
  delay(300);
  ESP.restart();
}

// Footer hint row inside a menu panel: "<downLbl> ↓  <rightLbl> →" with
// pixel triangles. Panels add MENU_HINT_H to height and call this at bottom.
const int MENU_HINT_H = 14;
static void drawMenuHints(const Palette& p, int mx, int mw, int hy,
                          const char* downLbl = "A", const char* rightLbl = "B") {
  spr.drawFastHLine(mx + 6, hy - 4, mw - 12, p.textDim);
  spr.setTextColor(p.textDim, PANEL);
  // 6px/glyph at size 1; triangle goes 4px after the label ends
  int x = mx + 8;
  spr.setCursor(x, hy); spr.print(downLbl);
  x += strlen(downLbl) * 6 + 4;
  spr.fillTriangle(x, hy + 1, x + 6, hy + 1, x + 3, hy + 6, p.textDim);
  x = mx + mw / 2 + 4;
  spr.setCursor(x, hy); spr.print(rightLbl);
  x += strlen(rightLbl) * 6 + 4;
  spr.fillTriangle(x, hy, x, hy + 6, x + 5, hy + 3, p.textDim);
}

static void drawSettings() {
  const Palette& p = characterPalette();
  int mw = UI_MENU_MW, mh = UI_MENU_HEADER_H + SETTINGS_N * UI_MENU_ROW_H + UI_MENU_HINT_H;
  int mx = (W - mw) / 2, my = (H - mh) / 2;
  spr.fillRoundRect(mx, my, mw, mh, 4, PANEL);
  spr.drawRoundRect(mx, my, mw, mh, 4, p.textDim);
  spr.setTextSize(1);
  Settings& s = settings();
  bool vals[] = { s.sound, s.bt, s.wifi, s.led, s.hud };
  int row0 = my + UI_MENU_HEADER_H / 2;
  for (int i = 0; i < SETTINGS_N; i++) {
    bool sel = (i == settingsSel);
    spr.setTextColor(sel ? p.text : p.textDim, PANEL);
    spr.setCursor(mx + 6, row0 + i * UI_MENU_ROW_H);
    spr.print(sel ? "> " : "  ");
    spr.print(settingsItems[i]);
    spr.setCursor(mx + mw - 36, row0 + i * UI_MENU_ROW_H);
    spr.setTextColor(p.textDim, PANEL);
    if (i == 0) {
      spr.printf("%u/4", brightLevel);
    } else if (i >= 1 && i <= 5) {
      spr.setTextColor(vals[i-1] ? GREEN : p.textDim, PANEL);
      spr.print(vals[i-1] ? " on" : "off");
    } else if (i == 6) {
      static const char* const RN[] = { "auto", "port", "land" };
      spr.print(RN[s.clockRot]);
    } else if (i == 7) {
      uint8_t total = buddySpeciesCount() + (gifAvailable ? 1 : 0);
      uint8_t pos   = buddyMode ? buddySpeciesIdx() + 1 : total;
      spr.printf("%u/%u", pos, total);
    }
  }
  drawMenuHints(p, mx, mw, my + mh - (UI_MENU_HINT_H - 2), "Next", "Change");
}

static void drawReset() {
  const Palette& p = characterPalette();
  int mw = UI_MENU_MW, mh = UI_MENU_HEADER_H + RESET_N * UI_MENU_ROW_H + UI_MENU_HINT_H;
  int mx = (W - mw) / 2, my = (H - mh) / 2;
  spr.fillRoundRect(mx, my, mw, mh, 4, PANEL);
  spr.drawRoundRect(mx, my, mw, mh, 4, HOT);
  spr.setTextSize(1);
  int row0 = my + UI_MENU_HEADER_H / 2;
  for (int i = 0; i < RESET_N; i++) {
    bool sel = (i == resetSel);
    spr.setTextColor(sel ? p.text : p.textDim, PANEL);
    spr.setCursor(mx + 6, row0 + i * UI_MENU_ROW_H);
    spr.print(sel ? "> " : "  ");
    bool armed = (i == resetConfirmIdx) &&
                 (int32_t)(millis() - resetConfirmUntil) < 0;
    if (armed) spr.setTextColor(HOT, PANEL);
    spr.print(armed ? "really?" : resetItems[i]);
  }
  drawMenuHints(p, mx, mw, my + mh - (UI_MENU_HINT_H - 2));
}

void menuConfirm() {
  switch (menuSel) {
    case 0: settingsOpen = true; menuOpen = false; settingsSel = 0; break;
    case 1: hwPowerOff(); break;
    case 2:
    case 3:
      menuOpen = false;
      displayMode = DISP_INFO;
      infoPage = (menuSel == 2) ? INFO_PG_BUTTONS : INFO_PG_CREDITS;
      applyDisplayMode();
      characterInvalidate();
      break;
    case 4: dataSetDemo(!dataDemo()); break;
    case 5: menuOpen = false; characterInvalidate(); break;
  }
}

void drawMenu() {
  const Palette& p = characterPalette();
  int mw = UI_MENU_MW, mh = UI_MENU_HEADER_H + MENU_N * UI_MENU_ROW_H + UI_MENU_HINT_H;
  int mx = (W - mw) / 2, my = (H - mh) / 2;
  spr.fillRoundRect(mx, my, mw, mh, 4, PANEL);
  spr.drawRoundRect(mx, my, mw, mh, 4, p.textDim);
  spr.setTextSize(1);
  int row0 = my + UI_MENU_HEADER_H / 2;
  for (int i = 0; i < MENU_N; i++) {
    bool sel = (i == menuSel);
    spr.setTextColor(sel ? p.text : p.textDim, PANEL);
    spr.setCursor(mx + 6, row0 + i * UI_MENU_ROW_H);
    spr.print(sel ? "> " : "  ");
    spr.print(menuItems[i]);
    if (i == 4) spr.print(dataDemo() ? "  on" : "  off");
  }
  drawMenuHints(p, mx, mw, my + mh - (UI_MENU_HINT_H - 2));
}

// Portrait-only clock on AMOLED port (landscape removed — 368×448 is
// near-square; rotating doesn't change the layout meaningfully).
static HwTime  _clkTm;
uint32_t       _clkLastRead = 0;   // zeroed by data.h on time-sync
static bool    _onUsb       = false;
static void clockRefreshRtc() {
  if (millis() - _clkLastRead < 1000) return;
  _clkLastRead = millis();
  _onUsb = hwBattery().usbPresent;
  hwRtcRead(&_clkTm);
}

// Clock face: shown when charging on USB with nothing else going on.
// Paints the upper ~110px to the canvas; pet renders below.
static const char* const MON[] = {
  "Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec"
};
static const char* const DOW[] = {"Sun","Mon","Tue","Wed","Thu","Fri","Sat"};

static uint8_t clockDow() { return _clkTm.dow % 7; }
// Manual centered-text helper (Arduino_GFX has no setTextDatum). Default
// font is 6 px wide × 8 px tall; multiply by textSize for placement.
static void drawCenteredText(const char* s, int cx, int cy, int sz, uint16_t fg, uint16_t bg) {
  int w = (int)strlen(s) * 6 * sz;
  int h = 8 * sz;
  spr.setTextSize(sz);
  spr.setTextColor(fg, bg);
  spr.setCursor(cx - w/2, cy - h/2);
  spr.print(s);
}
static void drawClock() {
  const Palette& p = characterPalette();
  char hms[12]; snprintf(hms, sizeof(hms), "%02u:%02u:%02u", _clkTm.H, _clkTm.M, _clkTm.S);
  uint8_t mi = (_clkTm.Mo >= 1 && _clkTm.Mo <= 12) ? _clkTm.Mo - 1 : 0;
  char dl[16]; snprintf(dl, sizeof(dl), "%s %s %02u", DOW[clockDow()], MON[mi], _clkTm.D);

  spr.fillRect(0, UI_CLOCK_TOP, W, UI_CLOCK_H, p.bg);
#if LANDSCAPE_UI
  // Landscape: size-2 time + size-1 date in the footer band.
  drawCenteredText(hms, CX, UI_CLOCK_TIME_CY, 2, p.text, p.bg);
  drawCenteredText(dl,  CX, UI_CLOCK_DATE_CY, 1, p.textDim, p.bg);
#else
  // Portrait: size-3 time above the date, leaving the pet above y=140.
  drawCenteredText(hms, CX, UI_CLOCK_TIME_CY, 3, p.text, p.bg);
  drawCenteredText(dl,  CX, UI_CLOCK_DATE_CY, 1, p.textDim, p.bg);
#endif
  spr.setTextSize(1);
}

PersonaState derive(const TamaState& s) {
  if (!s.connected)            return P_IDLE;
  if (s.sessionsWaiting > 0)   return P_ATTENTION;
  if (s.recentlyCompleted)     return P_CELEBRATE;
  if (s.sessionsRunning >= 3)  return P_BUSY;
  return P_IDLE;   // connected, 0+ sessions, nothing urgent — hang out
}

void triggerOneShot(PersonaState s, uint32_t durMs) {
  activeState = s;
  oneShotUntil = millis() + durMs;
}

bool checkShake() {
  float ax, ay, az;
  hwImuAccel(&ax, &ay, &az);
  float mag = sqrtf(ax*ax + ay*ay + az*az);
  float delta = fabsf(mag - accelBaseline);
  accelBaseline = accelBaseline * 0.95f + mag * 0.05f;
  return delta > 0.8f;
}




// Persistent screen-level title row ("INFO  n/3") matching the PET header,
// then a per-page section label below it. The fixed title is the cue that
// B cycles pages here just like it does on PET.
static void _infoHeader(const Palette& p, int& y, const char* section, uint8_t page) {
  spr.setTextColor(p.text, p.bg);
  spr.setCursor(SAFE_L, y); spr.print("Info");
  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_R - 24, y); spr.printf("%u/%u", page + 1, INFO_PAGES);
  y += 12;
  spr.setTextColor(p.body, p.bg);
  spr.setCursor(SAFE_L, y); spr.print(section);
  y += 12;
}

void drawPasskey() {
  const Palette& p = characterPalette();
  spr.fillScreen(p.bg);
  spr.setTextSize(1);
  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_L, 56);  spr.print("BLUETOOTH PAIRING");
  spr.setCursor(SAFE_L, SAFE_B - 32); spr.print("enter on desktop:");
  spr.setTextSize(3);
  spr.setTextColor(p.text, p.bg);
  char b[8]; snprintf(b, sizeof(b), "%06lu", (unsigned long)blePasskey());
  spr.setCursor((W - 18 * 6) / 2, 110);
  spr.print(b);
}

void drawInfo() {
  const Palette& p = characterPalette();
  spr.fillRect(0, UI_INFO_CLEAR_TOP, W, H - UI_INFO_CLEAR_TOP, p.bg);
  spr.setTextSize(1);
  int y = UI_INFO_TITLE_Y;
  auto ln = [&](const char* fmt, ...) {
    char b[32]; va_list a; va_start(a, fmt); vsnprintf(b, sizeof(b), fmt, a); va_end(a);
    spr.setCursor(SAFE_L, y); spr.print(b); y += 8;
  };

  if (infoPage == 0) {
    _infoHeader(p, y, "ABOUT", infoPage);
    spr.setTextColor(p.textDim, p.bg);
    ln("I watch your Claude");
    ln("desktop sessions.");
    y += 6;
    ln("I sleep when nothing's");
    ln("happening, wake when");
    ln("you start working,");
    ln("get impatient when");
    ln("approvals pile up.");
    y += 6;
    spr.setTextColor(p.text, p.bg);
    ln("Press A on a prompt");
    ln("to approve from here.");
    y += 6;
    spr.setTextColor(p.textDim, p.bg);
    ln("18 species. Settings");
    ln("> ascii pet to cycle.");

  } else if (infoPage == 1) {
    _infoHeader(p, y, "BUTTONS", infoPage);
    spr.setTextColor(p.text, p.bg);    ln("A   front");
    spr.setTextColor(p.textDim, p.bg); ln("    next screen");
    ln("    approve prompt"); y += 4;
    spr.setTextColor(p.text, p.bg);    ln("B   right side");
    spr.setTextColor(p.textDim, p.bg); ln("    next page");
    ln("    deny prompt"); y += 4;
    spr.setTextColor(p.text, p.bg);    ln("hold A");
    spr.setTextColor(p.textDim, p.bg); ln("    menu"); y += 4;
    spr.setTextColor(p.text, p.bg);    ln("Power  left side");
    spr.setTextColor(p.textDim, p.bg); ln("    tap = screen off");
    ln("    hold 6s = off");

  } else if (infoPage == 2) {
    _infoHeader(p, y, "CLAUDE", infoPage);
    spr.setTextColor(p.textDim, p.bg);
    ln("  sessions  %u", tama.sessionsTotal);
    ln("  running   %u", tama.sessionsRunning);
    ln("  waiting   %u", tama.sessionsWaiting);
    y += 8;
    spr.setTextColor(p.text, p.bg);
    ln("LINK");
    spr.setTextColor(p.textDim, p.bg);
    ln("  via       %s", dataScenarioName());
    ln("  ble       %s", !bleConnected() ? "-" : bleSecure() ? "encrypted" : "OPEN");
    uint32_t age = (millis() - tama.lastUpdated) / 1000;
    ln("  last msg  %lus", (unsigned long)age);
    ln("  state     %s", stateNames[activeState]);

  } else if (infoPage == 3) {
    _infoHeader(p, y, "DEVICE", infoPage);

    HwBattery hb = hwBattery();
    int vBat_mV  = hb.mV;
    int iBat_mA  = hb.mA;       // always 0 on AXP2101 (current not exposed)
    int vBus_mV  = hb.usbPresent ? 5000 : 0;
    int pct      = hb.pct;
    bool usb     = hb.usbPresent;
    bool charging = hb.charging;
    bool full    = usb && vBat_mV > 4100 && !charging;

    spr.setTextColor(p.text, p.bg);
    spr.setTextSize(2);
    spr.setCursor(SAFE_L, y);
    spr.printf("%d%%", pct);
    spr.setTextSize(1);
    spr.setTextColor(full ? GREEN : (charging ? HOT : p.textDim), p.bg);
    spr.setCursor(60, y + 4);
    spr.print(full ? "full" : (charging ? "charging" : (usb ? "usb" : "battery")));
    y += 20;

    spr.setTextColor(p.textDim, p.bg);
    ln("  battery  %d.%02dV", vBat_mV/1000, (vBat_mV%1000)/10);
    ln("  current  %+dmA", iBat_mA);
    if (usb) ln("  usb in   %d.%02dV", vBus_mV/1000, (vBus_mV%1000)/10);
    y += 8;

    spr.setTextColor(p.text, p.bg);
    ln("SYSTEM");
    spr.setTextColor(p.textDim, p.bg);
    if (ownerName()[0]) ln("  owner    %s", ownerName());
    uint32_t up = millis() / 1000;
    ln("  uptime   %luh %02lum", up / 3600, (up / 60) % 60);
    ln("  heap     %uKB", ESP.getFreeHeap() / 1024);
    ln("  bright   %u/4", brightLevel);
    ln("  bt       %s", settings().bt ? (dataBtActive() ? "linked" : "on") : "off");
    ln("  temp     %dC", (int)hwBattery().tempC);

  } else if (infoPage == 4) {
    _infoHeader(p, y, "BLUETOOTH", infoPage);
    bool linked = settings().bt && dataBtActive();

    spr.setTextColor(linked ? GREEN : (settings().bt ? HOT : p.textDim), p.bg);
    ln("%s", linked ? "linked" : (settings().bt ? "discover" : "off"));
    y += 4;

    spr.setTextColor(p.text, p.bg);
    ln("%s", btName);
    spr.setTextColor(p.textDim, p.bg);
    uint8_t mac[6] = {0};
    esp_read_mac(mac, ESP_MAC_BT);
    ln("%02X:%02X:%02X:%02X:%02X:%02X",
       mac[0],mac[1],mac[2],mac[3],mac[4],mac[5]);
    y += 4;

    if (linked) {
      uint32_t age = (millis() - tama.lastUpdated) / 1000;
      ln("last msg  %lus", (unsigned long)age);
    } else if (settings().bt) {
      spr.setTextColor(p.text, p.bg);
      ln("TO PAIR");
      spr.setTextColor(p.textDim, p.bg);
      ln("Open Claude desktop");
      ln("> Developer");
      ln("> Hardware Buddy");
      y += 4;
      ln("auto-connects via BLE");
    }

  } else {
    _infoHeader(p, y, "CREDITS", infoPage);
    spr.setTextColor(p.textDim, p.bg);
    ln("made by");
    y += 4;
    spr.setTextColor(p.text, p.bg);
    ln("Felix Rieseberg");
    y += 12;
    spr.setTextColor(p.textDim, p.bg);
    ln("hardware adaptation");
    y += 4;
    spr.setTextColor(p.text, p.bg);
    ln("yadong");
    y += 12;
    spr.setTextColor(p.textDim, p.bg);
    ln("hardware");
    y += 4;
    spr.setTextColor(p.text, p.bg);
    ln(BOARD_MODEL_LINE1);
    ln(BOARD_MODEL_LINE2);
  }
}


// Pixel width of the first `len` UTF-8 bytes of `s` in the currently
// selected font. Call after setFont/setTextSize so getTextBounds uses
// the right metrics. `len` may cut inside a codepoint; we null-terminate
// at `len` and let getTextBounds render up to that point.
static int measureTextWidth(Arduino_GFX* gfx, const char* s, uint8_t len) {
  char buf[92];
  if (len >= sizeof(buf)) len = sizeof(buf) - 1;
  memcpy(buf, s, len);
  buf[len] = 0;
  int16_t x1, y1;
  uint16_t w, h;
  gfx->getTextBounds(buf, 0, 0, &x1, &y1, &w, &h);
  return (int)w;
}

// Returns how many leading bytes of `w` fit in `roomPx` without splitting
// a UTF-8 codepoint. Uses the current font for accurate width.
static uint8_t _utf8SafeTakePx(Arduino_GFX* gfx, const char* w, int roomPx, uint8_t wlen) {
  uint8_t taken = 0;
  while (taken < wlen) {
    uint8_t c = (uint8_t)w[taken];
    uint8_t cl;
    if (c < 0x80) cl = 1;
    else if ((c & 0xE0) == 0xC0) cl = 2;
    else if ((c & 0xF0) == 0xE0) cl = 3;
    else if ((c & 0xF8) == 0xF0) cl = 4;
    else { taken++; continue; }                // invalid lead, skip
    if (taken + cl > wlen) break;
    int px = measureTextWidth(gfx, w, taken + cl);
    if (px > roomPx) break;
    taken += cl;
  }
  return taken;
}

// Greedy word-wrap into fixed-width rows. Continuation rows get a leading
// space. Returns number of rows written. `maxPx` is the pixel-width budget
// for each row, measured against the currently selected font.
static uint8_t wrapInto(Arduino_GFX* gfx, const char* in, char out[][48], uint8_t maxRows, int maxPx) {
  uint8_t row = 0, col = 0;
  int rowPx = 0;
  const char* p = in;
  while (*p && row < maxRows) {
    while (*p == ' ') p++;                     // skip leading spaces
    // measure next word
    const char* w = p;
    while (*p && *p != ' ') p++;
    uint8_t wlen = p - w;
    if (wlen == 0) break;
    int wordPx = measureTextWidth(gfx, w, wlen);
    int spacePx = 0;
    if (col > 0 && !(col == 1 && out[row][0] == ' ')) {
      // need a separating space; measure it rather than assume 6 px
      spacePx = measureTextWidth(gfx, " ", 1);
    }
    if (rowPx + spacePx + wordPx > maxPx) {
      out[row][col] = 0;
      if (++row >= maxRows) return row;
      out[row][0] = ' '; col = 1;              // continuation indent
      rowPx = measureTextWidth(gfx, " ", 1);
    }
    if (col > 1 || (col == 1 && out[row][0] != ' ')) {
      out[row][col++] = ' ';
      rowPx += measureTextWidth(gfx, " ", 1);
    }
    else if (col == 1 && row > 0) {}           // already have the indent space
    // hard-break words that still don't fit, on UTF-8 char boundaries
    while (wordPx > maxPx - rowPx) {
      int room = maxPx - rowPx;
      uint8_t take = _utf8SafeTakePx(gfx, w, room, wlen);
      if (take == 0) take = 1;                 // safety: avoid infinite loop
      memcpy(&out[row][col], w, take); col += take; w += take; wlen -= take;
      out[row][col] = 0;
      if (++row >= maxRows) return row;
      out[row][0] = ' '; col = 1;              // continuation indent
      rowPx = measureTextWidth(gfx, " ", 1);
      wordPx = measureTextWidth(gfx, w, wlen);
    }
    memcpy(&out[row][col], w, wlen); col += wlen;
    rowPx += wordPx;
  }
  if (col > 0 && row < maxRows) { out[row][col] = 0; row++; }
  return row;
}

static void drawApproval() {
  const Palette& p = characterPalette();
  const int AREA = UI_APPROVAL_H;
  const int TOP = H - AREA;
  spr.fillRect(0, TOP, W, AREA, p.bg);
  spr.drawFastHLine(0, TOP, W, p.textDim);

  spr.setTextSize(1);
  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_L, TOP + 4);
  uint32_t waited = (millis() - promptArrivedMs) / 1000;
  if (waited >= 10) spr.setTextColor(HOT, p.bg);
  spr.printf("approve? %lus", (unsigned long)waited);

  // Size 2 only if it fits one line.
  int toolLen = strlen(tama.promptTool);
  spr.setTextColor(p.text, p.bg);
  spr.setTextSize(toolLen <= 10 ? 2 : 1);
  spr.setCursor(SAFE_L, TOP + (toolLen <= 10 ? UI_APPROVAL_TOOL_Y_SHORT : UI_APPROVAL_TOOL_Y_LONG));
  spr.print(tama.promptTool);
  spr.setTextSize(1);

  spr.setTextColor(p.textDim, p.bg);
  int hlen = strlen(tama.promptHint);
#if LANDSCAPE_UI
  // Landscape: only one hint line fits; keep room for the A/B buttons.
  spr.setCursor(SAFE_L, TOP + UI_APPROVAL_HINT_Y);
  spr.printf("%.24s", tama.promptHint);
#else
  // Portrait: two-line hint.
  spr.setCursor(SAFE_L, TOP + UI_APPROVAL_HINT_Y);
  spr.printf("%.21s", tama.promptHint);
  if (hlen > 21) {
    spr.setCursor(SAFE_L, TOP + UI_APPROVAL_HINT_Y + 8);
    spr.printf("%.21s", tama.promptHint + 21);
  }
#endif
  const int actionY = TOP + UI_APPROVAL_ACTION_Y;

  if (responseSent) {
    spr.setTextColor(p.textDim, p.bg);
    spr.setCursor(SAFE_L, actionY);
    spr.print("sent...");
  } else {
    spr.setTextColor(GREEN, p.bg);
    spr.setCursor(SAFE_L, actionY);
    spr.print("A: approve");
    spr.setTextColor(HOT, p.bg);
    spr.setCursor(SAFE_R - 48, actionY);
    spr.print("B: deny");
  }
}

static void tinyHeart(int x, int y, bool filled, uint16_t col) {
  if (filled) {
    spr.fillCircle(x - 2, y, 2, col);
    spr.fillCircle(x + 2, y, 2, col);
    spr.fillTriangle(x - 4, y + 1, x + 4, y + 1, x, y + 5, col);
  } else {
    spr.drawCircle(x - 2, y, 2, col);
    spr.drawCircle(x + 2, y, 2, col);
    spr.drawLine(x - 4, y + 1, x, y + 5, col);
    spr.drawLine(x + 4, y + 1, x, y + 5, col);
  }
}

static void drawPetStats(const Palette& p) {
  spr.fillRect(0, UI_PET_CLEAR_TOP, W, H - UI_PET_CLEAR_TOP, p.bg);
  spr.setTextSize(1);
#if LANDSCAPE_UI
  // Compact landscape layout (content starts at UI_PET_TOP ≈ 18).
  int y = UI_PET_TOP + 6;

  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_L, y - 2); spr.print("mood");
  uint8_t mood = statsMoodTier();
  uint16_t moodCol = (mood >= 3) ? RED : (mood >= 2) ? HOT : p.textDim;
  for (int i = 0; i < 4; i++) tinyHeart(48 + i * 12, y + 2, i < mood, moodCol);

  y += 14;
  spr.setCursor(SAFE_L, y - 2); spr.print("fed");
  uint8_t fed = statsFedProgress();
  for (int i = 0; i < 10; i++) {
    int px = 36 + i * 7;
    if (i < fed) spr.fillCircle(px, y + 1, 2, p.body);
    else spr.drawCircle(px, y + 1, 2, p.textDim);
  }

  y += 14;
  spr.setCursor(SAFE_L, y - 2); spr.print("energy");
  uint8_t en = statsEnergyTier();
  uint16_t enCol = (en >= 4) ? 0x07FF : (en >= 2) ? 0xFFE0 : HOT;
  for (int i = 0; i < 5; i++) {
    int px = 46 + i * 10;
    if (i < en) spr.fillRect(px, y - 2, 7, 4, enCol);
    else spr.drawRect(px, y - 2, 7, 4, p.textDim);
  }

  y += 16;
  spr.fillRoundRect(SAFE_L, y - 2, 34, 12, 2, p.body);
  spr.setTextColor(p.bg, p.body);
  spr.setCursor(SAFE_L + 4, y + 1); spr.printf("Lv %u", stats().level);
  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_L + 42, y); spr.printf("a %u  d %u", stats().approvals, stats().denials);

  y += 14;
  uint32_t nap = stats().napSeconds;
  spr.setCursor(SAFE_L, y);
  spr.printf("napped %luh%02lum", nap/3600, (nap/60)%60);
  auto tokFmt = [&](const char* label, uint32_t v, int yPx) {
    spr.setCursor(SAFE_L, yPx);
    if (v >= 1000000)   spr.printf("%s%lu.%luM", label, v/1000000, (v/100000)%10);
    else if (v >= 1000) spr.printf("%s%lu.%luK", label, v/1000, (v/100)%10);
    else                spr.printf("%s%lu", label, v);
  };
  tokFmt("tokens ", stats().tokens, y + 12);
  tokFmt("today  ", tama.tokensToday, y + 24);
#else
  // Original portrait layout.
  int y = UI_PET_TOP + 16;

  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_L, y - 2); spr.print("mood");
  uint8_t mood = statsMoodTier();
  uint16_t moodCol = (mood >= 3) ? RED : (mood >= 2) ? HOT : p.textDim;
  for (int i = 0; i < 4; i++) tinyHeart(54 + i * 16, y + 2, i < mood, moodCol);

  y += 20;
  spr.setCursor(SAFE_L, y - 2); spr.print("fed");
  uint8_t fed = statsFedProgress();
  for (int i = 0; i < 10; i++) {
    int px = 38 + i * 9;
    if (i < fed) spr.fillCircle(px, y + 1, 2, p.body);
    else spr.drawCircle(px, y + 1, 2, p.textDim);
  }

  y += 20;
  spr.setCursor(SAFE_L, y - 2); spr.print("energy");
  uint8_t en = statsEnergyTier();
  uint16_t enCol = (en >= 4) ? 0x07FF : (en >= 2) ? 0xFFE0 : HOT;
  for (int i = 0; i < 5; i++) {
    int px = 54 + i * 13;
    if (i < en) spr.fillRect(px, y - 2, 9, 6, enCol);
    else spr.drawRect(px, y - 2, 9, 6, p.textDim);
  }

  y += 24;
  spr.fillRoundRect(SAFE_L, y - 2, 42, 14, 3, p.body);
  spr.setTextColor(p.bg, p.body);
  spr.setCursor(SAFE_L + 5, y + 1); spr.printf("Lv %u", stats().level);

  y += 20;
  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_L, y);
  spr.printf("approved %u", stats().approvals);
  spr.setCursor(SAFE_L, y + 10);
  spr.printf("denied   %u", stats().denials);
  uint32_t nap = stats().napSeconds;
  spr.setCursor(SAFE_L, y + 20);
  spr.printf("napped   %luh%02lum", nap/3600, (nap/60)%60);
  auto tokFmt = [&](const char* label, uint32_t v, int yPx) {
    spr.setCursor(SAFE_L, yPx);
    if (v >= 1000000)   spr.printf("%s%lu.%luM", label, v/1000000, (v/100000)%10);
    else if (v >= 1000) spr.printf("%s%lu.%luK", label, v/1000, (v/100)%10);
    else                spr.printf("%s%lu", label, v);
  };
  tokFmt("tokens   ", stats().tokens, y + 30);
  tokFmt("today    ", tama.tokensToday, y + 40);
#endif
}

static void drawPetHowTo(const Palette& p) {
  spr.fillRect(0, UI_PET_CLEAR_TOP, W, H - UI_PET_CLEAR_TOP, p.bg);
  spr.setTextSize(1);
#if LANDSCAPE_UI
  int y = UI_PET_TOP + 2;
  auto ln = [&](uint16_t c, const char* s) {
    spr.setTextColor(c, p.bg); spr.setCursor(SAFE_L, y); spr.print(s); y += 9;
  };
  ln(p.body,    "MOOD: approve fast = up");
  ln(p.textDim, "      deny lots = down");
  ln(p.body,    "FED: 50K tokens = lvl up");
  ln(p.body,    "ENERGY: face-down to nap");
  ln(p.textDim, "idle 30s -> off, btn -> wake");
  ln(p.textDim, "A:screens B:page");
  ln(p.textDim, "hold A: menu");
#else
  int y = UI_PET_TOP + 2;
  auto ln = [&](uint16_t c, const char* s) {
    spr.setTextColor(c, p.bg); spr.setCursor(SAFE_L, y); spr.print(s); y += 9;
  };
  auto gap = [&]() { y += 4; };

  y += 12;  // room for the PET header drawn by drawPet()

  ln(p.body,    "MOOD");
  ln(p.textDim, " approve fast = up");
  ln(p.textDim, " deny lots = down"); gap();

  ln(p.body,    "FED");
  ln(p.textDim, " 50K tokens =");
  ln(p.textDim, " level up + confetti"); gap();

  ln(p.body,    "ENERGY");
  ln(p.textDim, " face-down to nap");
  ln(p.textDim, " refills to full"); gap();

  ln(p.textDim, "idle 30s = off");
  ln(p.textDim, "any button = wake"); gap();

  ln(p.textDim, "A: screens  B: page");
  ln(p.textDim, "hold A: menu");
#endif
}

void drawPet() {
  const Palette& p = characterPalette();
  int y = UI_PET_TITLE_Y;

  if (petPage == 0) drawPetStats(p);
  else drawPetHowTo(p);

  // Header on top of whichever page drew — title left, counter right
  spr.setTextSize(1);
  spr.setTextColor(p.text, p.bg);
  spr.setCursor(SAFE_L, y + 2);
  if (ownerName()[0]) {
    spr.printf("%s's %s", ownerName(), petName());
  } else {
    spr.print(petName());
  }
  spr.setTextColor(p.textDim, p.bg);
  spr.setCursor(SAFE_R - 24, y + 2);
  spr.printf("%u/%u", petPage + 1, PET_PAGES);
}

void drawHUD() {
  if (tama.promptId[0]) { drawApproval(); return; }
  const Palette& p = characterPalette();
  // chill7 font: glyphs ~7 px tall but baseline-positioned (setCursor
  // is the baseline, not the top). Allow ~10 px line spacing.
  const int SHOW = UI_HUD_LINES, LH = UI_HUD_LH, WIDTH_PX = SAFE_W;
  const int AREA = SHOW * LH + 4;
  spr.fillRect(0, H - AREA, W, AREA, p.bg);

  // Menu/settings/reset should hide the HUD strip underneath — panels are
  // centered and don't cover the bottom 34 px on their own.
  if (menuOpen || settingsOpen || resetOpen) return;

  if (tama.lineGen != lastLineGen) { msgScroll = 0; lastLineGen = tama.lineGen; wake(); }

  // buddy/character ticks leave textsize at 2 (home scale); without
  // pinning it here the CJK font alternates between 1× and 2× every tick.
  spr.setTextSize(1);
  spr.setFont((const uint8_t*)u8g2_font_chill7_h_cjk);

  if (tama.nLines == 0) {
    spr.setTextColor(p.text, p.bg);
    spr.setCursor(SAFE_L, SAFE_B - 4);
    spr.print(tama.msg);
    spr.setFont((const GFXfont*)NULL);
    return;
  }

  static char disp[32][48];
  static uint8_t srcOf[32];
  uint8_t nDisp = 0;
  // The desktop bridge may split a single status sentence into short lines.
  // Re-join them with spaces and re-wrap to the full screen width so the
  // HUD uses the whole safe area instead of leaving 1/3 of it blank.
  {
    static char concatBuf[8 * 92 + 8];
    int pos = 0;
    for (uint8_t i = 0; i < tama.nLines; i++) {
      if (i > 0 && pos < (int)sizeof(concatBuf) - 1) concatBuf[pos++] = ' ';
      const char* s = tama.lines[i];
      while (*s && pos < (int)sizeof(concatBuf) - 1) concatBuf[pos++] = *s++;
    }
    concatBuf[pos] = 0;
    nDisp = wrapInto(&spr, concatBuf, disp, 32, WIDTH_PX);
    for (uint8_t j = 0; j < nDisp; j++) srcOf[j] = tama.nLines - 1;
  }

  uint8_t maxBack = (nDisp > SHOW) ? (nDisp - SHOW) : 0;
  if (msgScroll > maxBack) msgScroll = maxBack;

  int end = (int)nDisp - msgScroll;
  int start = end - SHOW; if (start < 0) start = 0;
  uint8_t newest = tama.nLines - 1;
  for (int i = 0; start + i < end; i++) {
    uint8_t row = start + i;
    bool fresh = (srcOf[row] == newest) && (msgScroll == 0);
    spr.setTextColor(fresh ? p.text : p.textDim, p.bg);
    spr.setCursor(SAFE_L, H - AREA + 8 + i * LH);   // +8 = baseline offset for 7-px font
    spr.print(disp[row]);
  }

  spr.setFont((const GFXfont*)NULL);

  if (msgScroll > 0) {
    spr.setTextSize(1);
    spr.setTextColor(p.body, p.bg);
    spr.setCursor(SAFE_R - 18, SAFE_B - 10);
    spr.printf("-%u", msgScroll);
  }
}

void setup() {
  hwInit();                  // Wire + expander + display + power + input + IMU + RTC + audio
  startBt();                 // BLE stays always-on
  applyBrightness();
  lastInteractMs = millis();
  statsLoad();
  settingsLoad();
  petNameLoad();
  buddyInit();

  characterInit(nullptr);    // scan /characters/ for whatever is installed
  gifAvailable = characterLoaded();
  // species NVS: 0..N-1 = ASCII species, 0xFF = use GIF (also the default,
  // so a fresh install lands on the GIF).
  buddyMode = !(gifAvailable && speciesIdxLoad() == SPECIES_GIF);
  applyDisplayMode();

  {
    const Palette& p = characterPalette();
    spr.fillScreen(p.bg);
    if (ownerName()[0]) {
      char line[40];
      snprintf(line, sizeof(line), "%s's", ownerName());
      drawCenteredText(line,      W/2, H/2 - UI_BOOT_Y_OFFSET, 2, p.text, p.bg);
      drawCenteredText(petName(), W/2, H/2 + UI_BOOT_Y_OFFSET, 2, p.body, p.bg);
    } else {
      drawCenteredText("Hello!",          W/2, H/2 - UI_BOOT_Y_OFFSET, 2, p.body,    p.bg);
      drawCenteredText("a buddy appears", W/2, H/2 + UI_BOOT_Y_OFFSET, 1, p.textDim, p.bg);
    }
    spr.setTextSize(1);
    hwDisplayPush();
    delay(1800);
  }

  // Boot screen is done: erase it from the canvas before the main loop takes
  // over.  The first home-screen frame only repaints its own regions (buddy
  // strip, GIF area, HUD, etc.), so any splash text outside those regions
  // would otherwise remain visible as ghosting.
  {
    const Palette& p = characterPalette();
    spr.fillScreen(p.bg);
  }
  characterInvalidate();
  if (buddyMode) buddyInvalidate();

  Serial.printf("buddy: %s\n", buddyMode ? "ASCII mode" : "GIF character loaded");
}

void loop() {
  hwInputUpdate();
  ;
  t++;
  uint32_t now = millis();

  dataPoll(&tama);
  if (statsPollLevelUp()) triggerOneShot(P_CELEBRATE, 3000);
  baseState = derive(tama);

  // After waking the screen, hold sleep for 12s so users see the wake-up
  // animation. Urgent states (attention, celebrate, busy) override this.
  if (baseState == P_IDLE && (int32_t)(now - wakeTransitionUntil) < 0) baseState = P_SLEEP;

  if ((int32_t)(now - oneShotUntil) >= 0) activeState = baseState;

  // Attention indicator: AMOLED red border flash (replaces M5StickC LED).
  hwBorderAlert(activeState == P_ATTENTION && settings().led
                && (now / 400) % 2 == 0);

  // shake → dizzy + force scenario advance
  if (now - lastShakeCheck > 50) {
    lastShakeCheck = now;
    if (!menuOpen && !screenOff && checkShake() && (int32_t)(now - oneShotUntil) >= 0) {
      wake();
      triggerOneShot(P_DIZZY, 2000);
      Serial.println("shake: dizzy");
    }
  }

  // BtnA: step through fake scenarios
  // Prompt arrival: beep, reset response flag
  bool hadPrompt = lastPromptId[0] != '\0';
  if (strcmp(tama.promptId, lastPromptId) != 0) {
    strncpy(lastPromptId, tama.promptId, sizeof(lastPromptId)-1);
    lastPromptId[sizeof(lastPromptId)-1] = 0;
    responseSent = false;
    if (tama.promptId[0]) {
      promptArrivedMs = millis();
      wake();
      beep(1200, 80);   // alert chirp
      // Jump to the approval screen no matter what was open — drawApproval
      // only runs from drawHUD which only runs in DISP_NORMAL.
      displayMode = DISP_NORMAL;
      menuOpen = settingsOpen = resetOpen = false;
      applyDisplayMode();
      characterInvalidate();
      if (buddyMode) buddyInvalidate();
    } else if (hadPrompt) {
      // Prompt dismissed. The approval overlay is taller than the normal HUD/
      // clock footer, so leftover ink can remain after the desktop clears the
      // prompt. Force a full canvas clear/redraw to erase it.
      if (buddyMode) {
        const Palette& p = characterPalette();
        spr.fillScreen(p.bg);
        buddyInvalidate();
      } else {
        characterInvalidate();
      }
    }
  }

  bool inPrompt = tama.promptId[0] && !responseSent;

  // Button-press wake. Track which button woke the screen so its full
  // press cycle (including long-press) is swallowed — you don't want
  // BtnA-to-wake to also cycle displayMode or open the menu.
  if (hwBtnA().isPressed || hwBtnB().isPressed) {
    if (screenOff) {
      if (hwBtnA().isPressed) swallowBtnA = true;
      if (hwBtnB().isPressed) swallowBtnB = true;
    }
    wake();
  }

  // Key3 long-press (~1s, AXP IRQ 0x04) toggles screen off — replaces
  // M5StickC's PWR short-press behaviour (we only have 2 buttons; short
  // press of Key3 is BtnB, so screen-toggle moves to long-press).
  // Very-long-press (6s) still powers off via AXP hardware.
  if (hwAxpBtnEvent() == 0x04) {
    if (screenOff) {
      wake();
    } else {
      hwDisplaySleep(true);
      screenOff = true;
    }
  }

  if (hwBtnA().pressedFor(600) && !btnALong && !swallowBtnA) {
    btnALong = true;
    beep(800, 60);
    if (resetOpen) { resetOpen = false; }
    else if (settingsOpen) { settingsOpen = false; characterInvalidate(); }
    else {
      menuOpen = !menuOpen;
      menuSel = 0;
      if (!menuOpen) characterInvalidate();
    }
    Serial.println(menuOpen ? "menu open" : "menu close");
  }
  if (hwBtnA().wasReleased) {
    if (!btnALong && !swallowBtnA) {
      if (inPrompt) {
        char cmd[96];
        snprintf(cmd, sizeof(cmd), "{\"cmd\":\"permission\",\"id\":\"%s\",\"decision\":\"once\"}", tama.promptId);
        sendCmd(cmd);
        responseSent = true;
        uint32_t tookS = (millis() - promptArrivedMs) / 1000;
        statsOnApproval(tookS);
        beep(2400, 60);
        if (tookS < 5) triggerOneShot(P_HEART, 2000);
      } else if (resetOpen) {
        beep(1800, 30);
        resetSel = (resetSel + 1) % RESET_N;
        resetConfirmIdx = 0xFF;
      } else if (settingsOpen) {
        beep(1800, 30);
        settingsSel = (settingsSel + 1) % SETTINGS_N;
      } else if (menuOpen) {
        beep(1800, 30);
        menuSel = (menuSel + 1) % MENU_N;
      } else {
        beep(1800, 30);
        displayMode = (displayMode + 1) % DISP_COUNT;
        applyDisplayMode();
      }
    }
    btnALong = false;
    swallowBtnA = false;
  }

  // BtnB: pet → heart
  if (hwBtnB().wasPressed) {
    if (swallowBtnB) { swallowBtnB = false; }
    else
    if (inPrompt) {
      char cmd[96];
      snprintf(cmd, sizeof(cmd), "{\"cmd\":\"permission\",\"id\":\"%s\",\"decision\":\"deny\"}", tama.promptId);
      sendCmd(cmd);
      responseSent = true;
      statsOnDenial();
      beep(600, 60);
    } else if (resetOpen) {
      beep(2400, 30);
      applyReset(resetSel);
    } else if (settingsOpen) {
      beep(2400, 30);
      applySetting(settingsSel);
    } else if (menuOpen) {
      beep(2400, 30);
      menuConfirm();
    } else if (displayMode == DISP_INFO) {
      beep(2400, 30);
      infoPage = (infoPage + 1) % INFO_PAGES;
    } else if (displayMode == DISP_PET) {
      beep(2400, 30);
      petPage = (petPage + 1) % PET_PAGES;
      applyDisplayMode();
    } else {
      beep(2400, 30);
      msgScroll = (msgScroll >= 30) ? 0 : msgScroll + 1;
    }
  }

  // ─── Touch (additive — buttons above already handled) ──────────────
  // Clocking = idle home screen with RTC synced; drives gesture routing:
  // HUD (!clocking) gets tap-to-pet, clocking gets horizontal-swipe-to-switch-species.
  bool tpClocking = displayMode == DISP_NORMAL
                 && !menuOpen && !settingsOpen && !resetOpen && !inPrompt
                 && tama.sessionsRunning == 0 && tama.sessionsWaiting == 0
                 && dataRtcValid();

  const HwTouch& tp = hwTouch();
  if (tp.justPressed) { _tpStartX = tp.x; _tpStartY = tp.y; _tpStartMs = millis(); }

  // Approval: tap upper half of the approval area = approve,
  //           tap lower half = deny.
  if (inPrompt) {
    const int APPROVAL_TOP = H - UI_APPROVAL_H;
    const int APPROVAL_HALF = UI_APPROVAL_H / 2;
    if (tap(0, APPROVAL_TOP,            W, APPROVAL_HALF)) {
      char cmd[96];
      snprintf(cmd, sizeof(cmd), "{\"cmd\":\"permission\",\"id\":\"%s\",\"decision\":\"once\"}", tama.promptId);
      sendCmd(cmd);
      responseSent = true;
      uint32_t tookS = (millis() - promptArrivedMs) / 1000;
      statsOnApproval(tookS);
      beep(2400, 60);
      if (tookS < 5) triggerOneShot(P_HEART, 2000);
    }
    if (tap(0, APPROVAL_TOP + APPROVAL_HALF, W, APPROVAL_HALF)) {
      char cmd[96];
      snprintf(cmd, sizeof(cmd), "{\"cmd\":\"permission\",\"id\":\"%s\",\"decision\":\"deny\"}", tama.promptId);
      sendCmd(cmd);
      responseSent = true;
      statsOnDenial();
      beep(600, 60);
    }
  } else if (menuOpen || settingsOpen || resetOpen) {
    // Tap a menu row → directly select + confirm. Reuses the layout
    // constants from drawMenu/drawSettings/drawReset.
    int n      = menuOpen ? MENU_N : settingsOpen ? SETTINGS_N : RESET_N;
    int mw     = UI_MENU_MW;
    int mh     = UI_MENU_HEADER_H + n * UI_MENU_ROW_H + UI_MENU_HINT_H;
    int mx     = (W - mw) / 2;
    int my     = (H - mh) / 2;
    int rowH   = UI_MENU_ROW_H;
    int rowsTop = my + UI_MENU_HEADER_H / 2;
    const HwTouch& t = hwTouch();
    if (t.justPressed && t.x >= mx && t.x < mx + mw &&
        t.y >= rowsTop && t.y < rowsTop + n * rowH) {
      int hit = (t.y - rowsTop) / rowH;
      if (hit >= 0 && hit < n) {
        beep(2400, 30);
        if (menuOpen)         { menuSel     = hit; menuConfirm(); }
        else if (settingsOpen){ settingsSel = hit; applySetting(hit); }
        else /* resetOpen */  { resetSel    = hit; applyReset(hit); }
      }
    }
  }
  // END of press-based approval/overlay taps. Below: release-based classifier
  // for DISP_NORMAL / DISP_PET / DISP_INFO (vertical swipe cycles mode,
  // horizontal swipe in clock mode cycles species, stationary tap routes
  // to region-specific actions). Approval and overlay menus are excluded
  // so an accidental drag can't mis-decide.

  if (tp.justReleased
      && !inPrompt && !menuOpen && !settingsOpen && !resetOpen
      && !napping && !screenOff) {
    int dx = (int)tp.x - _tpStartX;
    int dy = (int)tp.y - _tpStartY;
    uint32_t dt = millis() - _tpStartMs;

    if (abs(dy) >= 40 && abs(dy) > abs(dx) * 2 && dt < 500) {
      // Vertical swipe → advance one step in the flat 9-page cycle
      // (up = next, down = previous). Key1 keeps the coarser 3-mode cycle.
      beep(1800, 30);
      if (dy < 0) swipeNextPage();
      else        swipePrevPage();
    }
    else if (tpClocking && abs(dx) >= 40 && abs(dx) > abs(dy) * 2 && dt < 500) {
      // Horizontal swipe in clock mode → cycle species (unchanged behaviour).
      beep(2400, 30);
      if (dx > 0) nextPet(); else prevPet();
      _playfulUntil = millis() + PLAYFUL_MS;
    }
    else if (abs(dx) < 12 && abs(dy) < 12 && dt < 800) {
      // Stationary tap → route by press-start position.
#if LANDSCAPE_UI
      const int PAGE_COUNTER_H = UI_HEADER_H;
      const int PET_BODY_Y = 0, PET_BODY_H = H - UI_FOOTER_H;
#else
      const int PAGE_COUNTER_H = 70;
      const int PET_BODY_Y = 20, PET_BODY_H = 110;
#endif
      if (displayMode == DISP_INFO && tappedFrom(W - 60, 0, 60, PAGE_COUNTER_H)) {
        beep(2400, 30);
        infoPage = (infoPage + 1) % INFO_PAGES;
      }
      else if (displayMode == DISP_PET && tappedFrom(W - 60, 0, 60, PAGE_COUNTER_H)) {
        beep(2400, 30);
        petPage = (petPage + 1) % PET_PAGES;
        applyDisplayMode();
      }
      else if (displayMode == DISP_NORMAL && !tpClocking && tappedFrom(0, PET_BODY_Y, W, PET_BODY_H)) {
        // Tap buddy body → heart reaction (HUD; clock mode uses the block below).
        triggerOneShot(P_HEART, 2000);
        _playfulUntil = millis() + PLAYFUL_MS;
        characterInvalidate();
        if (buddyMode) buddyInvalidate();
        beep(2400, 50);
      }
      else if (displayMode == DISP_NORMAL && !tpClocking && tappedFrom(0, H - UI_FOOTER_H, W, UI_FOOTER_H)) {
        // Bottom strip → scroll transcript back (mirrors BtnB short-press).
        beep(2400, 30);
        msgScroll = (msgScroll >= 30) ? 0 : msgScroll + 1;
      }
      else if (tpClocking && _tpStartY < H - UI_CLOCK_H) {
        // Clock mode upper part = buddy region (lower part is clock digits).
        triggerOneShot(P_HEART, 2000);
        _playfulUntil = millis() + PLAYFUL_MS;
        characterInvalidate();
        if (buddyMode) buddyInvalidate();
        beep(2400, 50);
      }
    }
  }

  // blink bookkeeping

  // Charging clock: takes over the home screen when on USB power, no
  // overlays, no prompt, no live Claude data, and the RTC has been set
  // by the bridge. Pet sleeps underneath. Exit restores Y via
  // applyDisplayMode() so the next mode-switch isn't visually offset.
  clockRefreshRtc();   // 1Hz internal throttle; also caches _onUsb
  // Show the clock when nothing is happening — bridge heartbeat alone
  // doesn't count as activity (it's the only way to get the RTC synced).
  // Clock shows when Claude is idle and the RTC is synced — regardless
  // of USB power. On battery the screen still auto-offs after a longer
  // timeout (CLOCK_OFF_MS_BAT) so it doesn't drain forever.
  bool clocking = displayMode == DISP_NORMAL
               && !menuOpen && !settingsOpen && !resetOpen && !inPrompt
               && tama.sessionsRunning == 0 && tama.sessionsWaiting == 0
               && dataRtcValid();
  // Portrait-only clock on AMOLED port; landscape was removed.
  static bool wasClocking = false;
  if (clocking != wasClocking) {
    if (clocking) {
      // GIFs are tall (up to 140 px) — must shrink to fit above clock.
      // ASCII buddy at scale 2 reaches y≈126; clock starts at y=140
      // (compact single-line layout) so peek isn't needed and the pet
      // gets to keep its full size.
      characterSetPeek(true);
      buddySetPeek(false);
      // Clear the full canvas once on entry: buddy/clock both update
      // partial regions every frame, so any stale ink left behind from
      // the previous mode would persist forever.
      const Palette& cp = characterPalette();
      spr.fillScreen(cp.bg);
    } else {
      applyDisplayMode();
    }
    characterInvalidate();
    if (buddyMode) buddyInvalidate();
    wasClocking = clocking;
  }
  // Skip the time-of-day mood logic while a one-shot animation
  // (shake → dizzy, level-up → celebrate, fast-approve → heart) is
  // active — otherwise it would overwrite activeState immediately.
  if (clocking && (int32_t)(now - oneShotUntil) >= 0) {
    if ((int32_t)(now - _playfulUntil) < 0) {
      // Recently interacted with (pet tap / species swipe) — rotate through
      // awake animations instead of falling back to the time-of-day logic
      // that mostly picks P_SLEEP. Decays to normal after PLAYFUL_MS.
      static const PersonaState PLAYFUL[] = {
        P_IDLE, P_IDLE, P_HEART, P_IDLE, P_CELEBRATE, P_IDLE
      };
      activeState = PLAYFUL[(now / 5000) % 6];
    } else {
      // Ambient rhythm is SLEEP↔IDLE only. Emotional states (HEART, CELEBRATE,
      // DIZZY) are reactions — they fire from real events (shake, fast-approve,
      // level-up, pet tap, species swipe) via triggerOneShot / playful window,
      // never spontaneously from wall-clock mood.
      uint8_t h = _clkTm.H;
      if (h < 7 || h >= 22) activeState = (now/15000 % 8 == 0) ? P_IDLE  : P_SLEEP;
      else                  activeState = (now/12000 % 6 == 0) ? P_SLEEP : P_IDLE;
    }
  }

  static uint32_t lastPasskey = 0;
  uint32_t pk = blePasskey();
  if (pk && !lastPasskey) { wake(); beep(1800, 60); }
  lastPasskey = pk;

  if (napping || screenOff) {
    // skip canvas render — face-down or powered off
  } else if (buddyMode) {
    buddyTick(activeState);
  } else if (characterLoaded()) {
    characterSetState(activeState);
    characterTick();
  } else {
    const Palette& p = characterPalette();
    spr.fillScreen(p.bg);
    spr.setTextColor(p.textDim, p.bg);
    spr.setTextSize(1);
    if (xferActive()) {
      uint32_t done = xferProgress(), total = xferTotal();
      int ty = UI_TRANSFER_Y;
      spr.setCursor(SAFE_L, ty - 12);
      spr.print("installing");
      spr.setCursor(SAFE_L, ty);
      spr.printf("%luK / %luK", done/1024, total/1024);
      int barW = W - 16;
      spr.drawRect(SAFE_L, ty + 14, barW, 8, p.textDim);
      if (total > 0) {
        int fill = (int)((uint64_t)barW * done / total);
        if (fill > 1) spr.fillRect(SAFE_L + 1, ty + 15, fill - 1, 6, p.body);
      }
    } else {
      spr.setCursor(SAFE_L, UI_TRANSFER_Y);
      spr.print("no character loaded");
    }
  }
  if (!napping && !screenOff) {
    if (blePasskey()) drawPasskey();
    else if (clocking) drawClock();
    else if (displayMode == DISP_INFO) drawInfo();
    else if (displayMode == DISP_PET) drawPet();
    else if (settings().hud) drawHUD();
    if (resetOpen) drawReset();
    else if (settingsOpen) drawSettings();
    else if (menuOpen) drawMenu();
    hwDisplayPush();
  }

  // Face-down nap: dim immediately, pause animations, accumulate sleep time.
  // Skipped during approval — you're holding it to read, not sleeping it.
  // Exit needs sustained not-down so IMU noise at the threshold doesn't
  // bounce brightness between 8 and full every few frames.
  static int8_t faceDownFrames = 0;
  if (!inPrompt) {
    bool down = isFaceDown();
    if (down)       { if (faceDownFrames < 20) faceDownFrames++; }
    else            { if (faceDownFrames > -10) faceDownFrames--; }
  }

  if (!napping && faceDownFrames >= 15) {
    napping = true;
    napStartMs = now;
    hwDisplayBrightness(0);
    dimmed = true;
  } else if (napping && faceDownFrames <= -8) {
    napping = false;
    statsOnNapEnd((now - napStartMs) / 1000);
    statsOnWake();
    wake();
  }

  // millis() not the cached `now`: wake() runs after `now` is captured,
  // so now - lastInteractMs underflows when a button is held → flicker.
  // Auto-off rules:
  //   USB plugged: never (clock can stay visible indefinitely)
  //   Battery + clock visible: 5 min (CLOCK_OFF_MS_BAT)
  //   Battery + non-clock idle: 30 s (SCREEN_OFF_MS)
#if !BOARD_DISABLE_SLEEP
  if (!screenOff && !inPrompt && !_onUsb) {
    uint32_t idleMs    = millis() - lastInteractMs;
    uint32_t threshold = clocking ? CLOCK_OFF_MS_BAT : SCREEN_OFF_MS;
    if (idleMs > threshold) {
      hwDisplaySleep(true);
      screenOff = true;
    }
  }
#endif

  // AMOLED burn-in mitigation: every 5 min force a full canvas redraw.
  // OLED pixels degrade where they stay lit at constant value; redrawing
  // (rather than incremental updates) at least exercises every pixel for
  // a frame. A more aggressive 1-px shimmy could shift the whole canvas
  // each cycle, but this minimum is a safe baseline.
  static uint32_t lastShimmy = 0;
  if (millis() - lastShimmy > 5UL * 60UL * 1000UL) {
    lastShimmy = millis();
    characterInvalidate();
    if (buddyMode) buddyInvalidate();
  }

  // LTPO-lite: vary loop cadence by what's happening. Animations tick on
  // wall-clock (buddy.cpp TICK_MS=200) and redraws are gated, so slowing the
  // loop during ambient SLEEP↔IDLE costs no frames — just fewer MCU wakes.
  // Fast rate only where latency is felt: input, interactive UI, one-shots,
  // nap-exit, transfer progress, BLE pairing.
  uint32_t loopMs;
  if (screenOff) {
    loopMs = 200;
  } else if (napping
          || hwTouch().down
          || hwBtnA().isPressed || hwBtnB().isPressed
          || inPrompt || menuOpen || settingsOpen || resetOpen
          || (int32_t)(now - oneShotUntil) < 0
          || xferActive()
          || blePasskey()) {
    loopMs = 16;
  } else {
    loopMs = 100;
  }
  // Slice the idle sleep so a touch-down IRQ (edge-triggered) or a button
  // press breaks out within ~8ms instead of waiting the full loopMs. Without
  // this, first-tap latency during idle felt sluggish.
  if (loopMs <= 16) {
    delay(loopMs);
  } else {
    uint32_t slept = 0;
    while (slept < loopMs) {
      uint32_t slice = (loopMs - slept > 8) ? 8 : (loopMs - slept);
      delay(slice);
      slept += slice;
      if (hwTouchIrqPending()) break;
      if (digitalRead(PIN_KEY1) == LOW) break;
    }
  }
}
